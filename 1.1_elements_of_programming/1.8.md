# Exercise 1.8

> Newton's method for cube roots is based on the fact that if _y_ is an approximation to the cube root of _x_, then a better approximation is given by the value 

> (_x_ / _y_Â² + 2 _y_) / 3

> Use this formula to implement a cube-root procedure analogous to the square-root procedure.

In `sqrt-iter`, we average our guess _y_ with _x_/_y_ to find a better guess. The guess procedure in that instance calculates `guess = (y + x/y) / 2`

```scheme
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))

(define (square x)
  (* x x))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (average x y)
  (/ (+ x y) 2))

(define (improve guess x)
  (average guess (/ x guess)))

(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))
```

To iteratively guess the cube root, we update the `improve` procedure, and add a `cube` procudure to check our guesses.

```scheme
(define (abs x)
  (cond ((< x 0) (- x))
        (else x)))

(define (square x)
  (* x x))

; add a method to determine cube
(define (cube x)
  (* x x x))

(define (good-enough? guess x)
  (< (abs (- (cube guess) x)) 0.001))

; update `improve` to find better approximations for cube roots
(define (improve guess x)
  (/ (+ (/ x (square guess))
        (* 2 guess))
     3))

(define (cbrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (cbrt-iter (improve guess x)
                 x)))

(define (cbrt x)
  (cbrt-iter 1.0 x))

(cbrt 27)
```
